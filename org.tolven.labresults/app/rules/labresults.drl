package labresults

// See http://wiki.tolven.org/doc/index.php/Plugin:org.tolven.component.tolvenejb#PlaceholderFacts
// for details on the following annotation:

/* Tolven */
import org.tolven.trim.*;
import org.tolven.trim.ex.*;
import org.tolven.app.entity.MenuStructure;
import org.tolven.app.entity.MenuData;

global org.tolven.app.AppEvalAdaptor app;

/**
 * If we find a lab result resolved, add it to the resolved observation list. 
 */
rule "Place lab result on lab result resolved list"
	when
		$msLabResult: MenuStructure( role == "placeholder", path == "echr:patient:labresult" )
		$mdLabResult: MenuData( menuStructure == $msLabResult, actStatus == "completed" )
		$msLabResultList: MenuStructure( role == "list", path == "echr:patient:labresults:completed" )		
	then
		app.createReferenceMD( $mdLabResult, $msLabResultList );
end
/**
 * If we find a lab result placeholder, add it to the lab result summary. 
 */
rule "Place lab result on lab result summary list"
	when
		$msLabResult: MenuStructure( role == "placeholder", path == "echr:patient:labresult" )
		$mdLabResult: MenuData( menuStructure == $msLabResult, actStatus == "completed" )
		$msLabResultSummary: MenuStructure( role == "list", path == "echr:patient:summary:labresultsum" )		
	then
		app.createReferenceMD( $mdLabResult, $msLabResultSummary );
end

/**
 * Historical items are not present in working memory unless we request them.
 * Harvest patient list entry for this patient (if processing any kind of patient data)
 */
rule "Harvest patient order for the result item"
	when	
        $trim: Trim( name matches "docclin/evn/shareLabResultIn" )	
		$msLabResult: MenuStructure( role == "placeholder", path == "echr:patient:labresult" )
		$mdLabResult: MenuData( menuStructure == $msLabResult, actStatus == "completed", $mdOrder: parent02!=null )
		$msLabResultList: MenuStructure( role == "list", path == "echr:patient:labOrder:results" )		
	then
		app.info("Executing rule: Harvest patient order for the result item");	
		insert($mdOrder);
        app.createReferenceMD( $mdLabResult, $msLabResultList, $mdOrder, false );		
end

/**
 * If we find a lab result with lab order as parent field, add it to the order:results list. 
 */
rule "Place lab result on lab order results list"
	when
        $trim: Trim( name matches "labResultDoc" )		
		$msLabResult: MenuStructure( role == "placeholder", path == "echr:patient:labresult" )
		$mdLabResult: MenuData( menuStructure == $msLabResult, actStatus == "completed", $mdOrder: parent02!=null )
		$msLabResultList: MenuStructure( role == "list", path == "echr:patient:labOrder:results" )		
	then
        app.info("Executing rule: Place lab result on lab order results list");	
		app.createReferenceMD( $mdLabResult, $msLabResultList );
end


